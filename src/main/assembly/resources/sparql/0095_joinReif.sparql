# Join through relation property
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bk: <http://www.ondex.org/bioknet/terms/>
PREFIX bkr: <http://www.ondex.org/bioknet/resources/>
PREFIX bka: <http://www.ondex.org/bioknet/attributes/>

SELECT DISTINCT ?pwName ?protName ?relatedConcept ?relatedType ?relatedName
{
  VALUES ( ?relType ) {
    ( bk:pd_by )
    ( bk:cs_by )
  }
  ?prot a bk:Protein.
  ?prot ?relType ?react.
  ?react a bk:Reaction.
  ?react bk:part_of ?pw.
  ?pw a bk:Path.

  ?rel bk:relFrom ?prot;
       bk:relTo ?react;
       bk:relTypeRef ?relType.

  ?rel bk:relatedConcept ?relatedConcept.
  ?relatedConcept a ?relatedType.

  OPTIONAL { ?pw bk:prefName ?pwName }
  OPTIONAL { ?prot bk:prefName ?protName }
  OPTIONAL { ?relatedConcept bk:prefName ?relatedName }
}
LIMIT 100
