# 2 unions, 1 nesting (with Cypher CALL)
#
# This is exactly the same of 2union1Nest, it is here just to allow the chart renderer 
# to pair the same union made with the new CALL method in Cypher.
#
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bk: <http://knetminer.org/data/rdf/terms/biokno/>
PREFIX bkr: <http://knetminer.org/data/rdf/resources/>
PREFIX bka: <http://knetminer.org/data/rdf/terms/biokno/attributes/>
PREFIX bkg: <http://knetminer.org/data/rdf/resources/graphs/>

SELECT ?pname ?ename ?rtype ?cname
# DEBUG FROM bkg:poaceae
{
  {
    ?e a bk:Enzyme; ^bk:is_a ?p.
    ?p a bk:Protein.
    BIND ( "is_a" AS ?rtype )
  }
  UNION {
    ?e a bk:Enzyme; bk:ac_by ?p.
    ?p a bk:Protein.
    BIND ( "ac_by" AS ?rtype )
  }

  ?e bk:ac_by|bk:in_by ?c.
  ?c a bk:Comp.
  
  OPTIONAL { ?p bk:prefName ?pname }
  OPTIONAL { ?e bk:prefName ?ename }
  OPTIONAL { ?c bk:prefName ?cname }
}
ORDER BY RAND()
LIMIT 100
