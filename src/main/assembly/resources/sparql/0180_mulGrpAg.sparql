# Multiple subqueries having aggregations 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bk: <http://knetminer.org/data/rdf/terms/biokno/>
PREFIX bkr: <http://knetminer.org/data/rdf/resources/>
PREFIX bka: <http://knetminer.org/data/rdf/terms/biokno/attributes/>
PREFIX bkg: <http://knetminer.org/data/rdf/resources/graphs/>

SELECT DISTINCT ?pwName ?type ?n
#Â DEBUG FROM bkg:poaceae
{
  {
  	# Two ways to transport
    SELECT ?pw ( COUNT ( DISTINCT ?prot ) AS ?n ) ( "enz" AS ?type )
    {
      ?prot a bk:Protein.
      ?prot bk:is_a|^bk:ac_by ?enz.
      
      ?enz a bk:Enzyme.
      {
        # enz/cmp/trn branch
        ?enz bk:ac_by|bk:in_by ?cmp.
      
        ?cmp a bk:Comp.
        ?cmp bk:cs_by|bk:pd_by ?trn.
      }
      UNION {
        # enz/trn branch
        ?trn bk:ca_by ?enz.
      }
      
			# From either transport to pathway
      ?trn a bk:Transport.
      
      ?trn bk:part_of ?pw.
      ?pw a bk:Path
    }
    GROUP BY ?pw
  }

  UNION 
  {
	  # To pathway, via protein and reaction
    SELECT ?pw ( COUNT ( DISTINCT ?prot ) AS ?n ) ( "react" AS ?type )
    {
      # react branch
      ?prot a bk:Protein.      
      ?prot bk:pd_by|bk:cs_by ?react.
      
      ?react a bk:Reaction.
      
      ?react bk:part_of ?pw.
      ?pw a bk:Path
    }
    GROUP BY ?pw
  }
  
  OPTIONAL { ?pw bk:prefName ?pwName }
}
LIMIT 100
