# 2 unions, no nesting
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bk: <http://knetminer.org/data/rdf/terms/biokno/>
PREFIX bkr: <http://knetminer.org/data/rdf/resources/>
PREFIX bka: <http://knetminer.org/data/rdf/terms/biokno/attributes/>

SELECT ?pname ?ename ?cname ?rtype
{
  { ?p a bk:Protein;
     bk:is_a ?e.

    ?e a bk:Enzyme.
    ?e bk:ac_by|bk:in_by ?c.

    ?c a bk:Comp.
    BIND ( "is_a" AS ?rtype )
  }
  UNION
  { ?e a bk:Enzyme;
      bk:ac_by ?p;
      bk:ac_by|bk:in_by ?c.

    ?p a bk:Protein.
    ?c a bk:Comp
    BIND ( "ac_by" AS ?rtype )
  }
  OPTIONAL { ?p bk:prefName ?pname }
  OPTIONAL { ?e bk:prefName ?ename }
  OPTIONAL { ?c bk:prefName ?cname }
}
LIMIT 100
