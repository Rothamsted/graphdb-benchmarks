# [aggregation] Group by + 2 aggregation functions
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bk: <http://knetminer.org/data/rdf/terms/biokno/>
PREFIX bkr: <http://knetminer.org/data/rdf/resources/>
PREFIX bka: <http://knetminer.org/data/rdf/terms/biokno/attributes/>
PREFIX bkg: <http://knetminer.org/data/rdf/resources/graphs/>

SELECT ?pwName ( AVG ( ?nReactProts ) AS ?avgReactProts )
#Â DEBUG FROM bkg:poaceae
{
  SELECT ?pwName ?react ( COUNT ( DISTINCT ?prot ) AS ?nReactProts )
  {
    ?prot a bk:Protein.
    ?prot bk:pd_by|bk:cs_by ?react.
    
    ?react a bk:Reaction.
    ?react bk:part_of ?pw.
    
    ?pw a bk:Path.
    ?pw bk:prefName ?pwName
  }
  GROUP BY ?pwName ?react
}
GROUP BY ?pwName
LIMIT 100
