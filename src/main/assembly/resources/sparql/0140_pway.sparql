# Complex union of paths over pathways
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bk: <http://knetminer.org/data/rdf/terms/biokno/>
PREFIX bkr: <http://knetminer.org/data/rdf/resources/>
PREFIX bka: <http://knetminer.org/data/rdf/terms/biokno/attributes/>
PREFIX bkg: <http://knetminer.org/data/rdf/resources/graphs/>

SELECT ?protName ?pwName ?branch
{ 
  ?prot a bk:Protein.
  {
    ?prot bk:is_a|^bk:ac_by ?enz.
    ?enz a bk:Enzyme.
    
    {
      BIND ( "enz/cmp/trn" AS ?branch ) # starting from prot
      
      ?enz bk:ac_by|bk:in_by ?cmp.
      ?cmp a bk:Comp.
      
      ?cmp bk:cs_by|bk:pd_by ?trn.
    }
    UNION {
      
      BIND ( "enz/trn" AS ?branch ) # another branch starting from prot
      ?enz ^bk:ca_by ?trn.
    }
    
    # trn -> pathway
    ?trn a bk:Transport.
    ?trn bk:part_of ?pw
  }
  UNION
  {
    # prot -> react -> pathway
     
    BIND ( "react" AS ?branch )
    
    ?prot bk:pd_by|bk:cs_by ?react.
    
    ?react a bk:Reaction.
    ?react bk:part_of ?pw.
  }
  ?pw a bk:Path

  OPTIONAL { ?prot bk:prefName ?protName }
  OPTIONAL { ?pw bk:prefName ?pwName }
}
ORDER BY RAND()
LIMIT 100
