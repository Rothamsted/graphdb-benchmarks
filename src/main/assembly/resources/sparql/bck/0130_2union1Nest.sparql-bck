# 2 unions, 1 nesting
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bk: <http://knetminer.org/data/rdf/terms/biokno/>
PREFIX bkr: <http://knetminer.org/data/rdf/resources/>
PREFIX bka: <http://knetminer.org/data/rdf/terms/biokno/attributes/>

SELECT ?pname ?ename ?rtype ?cname
FROM <http://knetminer.org/wheat>
{
  {
    ?p a bk:Protein; bk:is_a ?e.
    ?e a bk:Enzyme.
    BIND ( "is_a" AS ?rtype )
  } UNION {
    ?e a bk:Enzyme; bk:ac_by ?p.
    ?p a bk:Protein.
    BIND ( "ac_by" AS ?rtype )
  }

  ?e bk:ac_by|bk:in_by ?c.
  
  OPTIONAL { ?p bk:prefName ?pname }
  OPTIONAL { ?e bk:prefName ?ename }
  OPTIONAL { ?c bk:prefName ?cname }
}
LIMIT 100
